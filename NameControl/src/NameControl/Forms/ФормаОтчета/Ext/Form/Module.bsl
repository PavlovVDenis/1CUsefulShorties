
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СкрытьВсеРезультаты = Истина;
	ОбновитьВидимостьРезультатаНаСервере(Ложь, СкрытьВсеРезультаты);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИмяПеременнойОчистка(Элемент, СтандартнаяОбработка)
	СкрытьВсеРезультаты = Истина;
	ОбновитьВидимостьРезультатаНаСервере(Ложь, СкрытьВсеРезультаты);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьИмяПеременной(Команда)
	ПроверитьИмяПеременнойНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьВидимостьРезультатаНаСервере(Знач ЭтоКорректноеИмя, Знач СкрытьВсе = Ложь)
	
	ПоказатьКорректный		= ?(СкрытьВсе, Ложь, ЭтоКорректноеИмя);
	ПоказатьНекорректный	= ?(СкрытьВсе, Ложь, НЕ ЭтоКорректноеИмя);
	
	Элементы.ГруппаРезультатКорректный.Видимость 	= ПоказатьКорректный;
	Элементы.ГруппаРезультатНекорректный.Видимость 	= ПоказатьНекорректный;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьИмяПеременнойНаСервере()
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	ЭтоКорректноеИмя = ОтчетОбъект.ЭтоКорректноеИмяПеременной(СокрЛП(ИмяПеременной));
	
	ОбновитьВидимостьРезультатаНаСервере(ЭтоКорректноеИмя);
	
КонецПроцедуры

#КонецОбласти